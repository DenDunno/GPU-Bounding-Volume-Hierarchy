#pragma kernel GlobalScatter
#pragma kernel ChunkSort
#define ALL_POSSIBLE_VALUES 4 // SORTED_BITS_PER_PASS ^ 2
#define PREFIX_SUM_TYPE int4
#define THREADS 256
#include "RadixSortBlockSum.hlsl"
#include "IntegerRadixSort.hlsl"
#include "RadixSortInput.hlsl"
#include "../Common/ThreadId.hlsl"
#include "../PrefixSum/HillisAndSteelePrefixSum.hlsl"
#include "..//Utilities/ThreadsUtilities.hlsl"

RWStructuredBuffer<SORTING_TYPE> LocalShuffle;
RWStructuredBuffer<int> LocalPrefixSum;
groupshared SORTING_TYPE SortedChunk[THREADS];
groupshared int LocalPrefixSumChunk[THREADS];
groupshared int4 OffsetTable; // int4 for optimization, int[ALL_POSSIBLE_VALUES = 4]

void BuildOffsetTableForLocalScattering(const int4 totalElementsInGroup)
{
    int4 offsetTable = 0;

    [unroll(ALL_POSSIBLE_VALUES - 1)]
    for (int sortValue = 1; sortValue < ALL_POSSIBLE_VALUES; ++sortValue)
    {
        offsetTable[sortValue] = totalElementsInGroup[sortValue - 1] +
                                 offsetTable[sortValue - 1];
    }

    OffsetTable = offsetTable;
}

void WriteToBlockSum(const int groupId, const int4 totalElementsInGroup)
{
    [unroll(ALL_POSSIBLE_VALUES)]
    for (int sortValue = 0; sortValue < ALL_POSSIBLE_VALUES; ++sortValue)
    {
        WriteToBlockSum(sortValue, groupId, totalElementsInGroup[sortValue]);
    }
}

void PerformLocalScatter(const RadixSortInput input, const ThreadId id)
{
    GroupMemoryBarrierWithGroupSync();
    const int targetElementPrefixSum = LocalPrefixSumChunk[id.Local];
    const int totalElementsLessThanTargetElement = OffsetTable[input.ExtractedBits];
    const int localScatterIndex = totalElementsLessThanTargetElement + targetElementPrefixSum;
    GroupMemoryBarrierWithGroupSync();

    if (IsInBounds(id.Global))
    {
        LocalPrefixSumChunk[localScatterIndex] = targetElementPrefixSum;
        SortedChunk[localScatterIndex] = input.InitialValue;
    }

    GroupMemoryBarrierWithGroupSync();

    if (IsInBounds(id.Global))
    {
        LocalPrefixSum[id.Global] = LocalPrefixSumChunk[id.Local];
        LocalShuffle[id.Global] = SortedChunk[id.Local];
    }
}

void HandleLastLocalThread(const ThreadId id, const int4 hasPassedMask, const int4 scanPerElement)
{
    if (id.Local == THREAD_LAST_INDEX)
    {
        const int4 totalElementsInGroup = scanPerElement + hasPassedMask;
        BuildOffsetTableForLocalScattering(totalElementsInGroup);
        WriteToBlockSum(id.Group, totalElementsInGroup);
    }
}

void BuildLocalPrefixSumChunk(const int threadId, const int4 hasPassedMask, const int4 scanPerElement)
{
    [unroll(ALL_POSSIBLE_VALUES)]
    for (int sortValue = 0; sortValue < ALL_POSSIBLE_VALUES; ++sortValue)
    {
        int sortValueExclusiveScan = scanPerElement[sortValue];

        if (hasPassedMask[sortValue])
        {
            LocalPrefixSumChunk[threadId] = sortValueExclusiveScan;
        }
    }
}

[numthreads(THREADS,1,1)]
void GlobalScatter(const int globalId : SV_DispatchThreadID, const int groupId : SV_GroupID)
{
    if (IsOutOfBounds(globalId)) return;

    const SORTING_TYPE bufferValue = LocalShuffle[globalId];
    const int sortValue = ExtractBits(bufferValue);

    const int blockSumPrefixSum = ReadBlockSumPrefixSum(sortValue, groupId);
    const int localPrefixSum = LocalPrefixSum[globalId];
    const int globalScatterPosition = localPrefixSum + blockSumPrefixSum;

    Input[globalScatterPosition] = bufferValue;
}

[numthreads(THREADS,1,1)]
void ChunkSort(const int globalId : SV_DispatchThreadID, const int threadId : SV_GroupThreadID, const int groupId : SV_GroupID)
{
    const ThreadId id = ThreadId::Create(globalId, threadId, groupId);
    const RadixSortInput input = RadixSortInput::Fetch(id.Global);
    const int4 scanPerElement = ComputeExclusiveScan(input.HasPassedMask, id.Local);
    BuildLocalPrefixSumChunk(id.Local, input.HasPassedMask, scanPerElement);
    HandleLastLocalThread(id, input.HasPassedMask, scanPerElement);
    PerformLocalScatter(input, id);
    // __SynchronizeAllThreads(threadId, groupId, THREADS, SortLength);
    // PerformBlockSumPrefixSum(globalId);
    // __SynchronizeAllThreads(threadId, groupId, THREADS, SortLength);
    // GlobalScatter(globalId, groupId);
}