#pragma kernel Build
#pragma enable_d3d11_debug_symbols
#include "BinaryRadixTree.hlsl"
#include "ConstructionResult.hlsl"

void Merge(const int parentId)
{
    BVHNode node = Nodes[parentId];
    BVHNode leftChild = Nodes[node.LeftChild()];
    BVHNode rightChild = Nodes[node.RightChild()];
    node.Box = leftChild.Box.Union(rightChild.Box);

    Nodes[parentId] = node;
}

ConstructionResult RunHPLOC(uint id)
{
    bool isThreadActive = IsInBounds(id);
    Range range = Range::Create(id);

    while (isThreadActive && IsNotRoot(range.Size()))
    {
        ParentInfo parentInfo = ChooseParent(id, range);
        isThreadActive = parentInfo.HaveBothChildrenBeenProcessed();

        if (isThreadActive)
        {
            range = parentInfo.Range;
            id = parentInfo.Id;
            Merge(parentInfo.Id);
        }
    }
    
    return ConstructionResult::Create(id, range.Size());
}

[numthreads(1,1,1)]
void Build(const uint globalId : SV_DispatchThreadID)
{
    ConstructionResult result = RunHPLOC(globalId);
    TrySetRoot(result.NodeIndex, result.RangeSize);
}