#pragma kernel SinglePrefixSum
#pragma kernel ChunkPrefixSum
#pragma kernel BlockSumAddition
#include "..//..//Threads.hlsl"
#include "BlellochPrefixSum.hlsl"

RWStructuredBuffer<int> Result;

[numthreads(THREADS,1,1)]
void SinglePrefixSum(uint3 id : SV_DispatchThreadID, uint3 threadId : SV_GroupThreadID)
{
    Result[id.x] = ComputeExclusivePrefixSum(Result[id.x], threadId.x);
}

[numthreads(THREADS,1,1)]
void ChunkPrefixSum(uint3 id : SV_DispatchThreadID, uint3 groupId : SV_GroupID, uint3 threadId : SV_GroupThreadID)
{
    int inputValue = Result[id.x];
    int exclusivePrefixSum = ComputeExclusivePrefixSum(Result[id.x], threadId.x);
    Result[id.x] = exclusivePrefixSum;
    
    if (threadId.x == THREAD_LAST_INDEX)
    {
        int totalInGroup = exclusivePrefixSum + inputValue;
        ScanBlockSum[groupId.x] = totalInGroup; 
    }
}

[numthreads(THREADS,1,1)]
void BlockSumAddition(uint3 id : SV_DispatchThreadID, uint3 groupId : SV_GroupID)
{
    Result[id.x] += ScanBlockSum[groupId.x];
}